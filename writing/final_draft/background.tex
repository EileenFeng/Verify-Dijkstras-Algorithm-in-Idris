%-----------------------------------------------------------------------------------------------
% Background
%-----------------------------------------------------------------------------------------------
\subsection{Introduction of Idris}
Idris is a general-purpose functional programming language with dependent types. Many aspects of Idris are influenced by Haskell and ML. Features of Idris include but not limit to dependent types, \inl{with} rule, \inl{case} expression, and interactive editing. 

\subsubsection*{Variables and Types}
Idris requires type declarations for all variables and functions defined. To define a variable, we provide the type on one line, and specify the value on the next line. Below presents the syntax for variable declaration. 
\begin{lstlisting}
      <variable_name> : <type> 
      <variable_name> = <value>
\end{lstlisting}
The example below defines a variable \inl{n} of type \inl{Int} with value $37$. 
\begin{lstlisting}
      n : Int
      n = 37
\end{lstlisting}

Types in Idris are first-class values, which means types can be operated as any other values. Type declaration is the same as declaring any other variables, with exactly the same syntax, except that the type of a type is \inl{Type}. By convention, variables that represent types are capitalized. Below example declares a type \st{CharList}, which denotes the type of list of characters. 
\begin{lstlisting}
      CharList : Type
      CharList = List Char
\end{lstlisting}

\inl{CharList} is a type that stands for \inl{List} of \inl{Char}s, and declaring a variable of type \inl{CharList} is the same as declaring a variable of type \inl{List Char}. The following example declares a variable \inl{lisChar} of type \inl{CharList}. \inl{lisChar} contains the characters for the English word "hello". 
\begin{lstlisting}
      lisChar : CharList 
      lisChar = `h' :: `e' :: `l' :: `l' :: `o' :: Nil
\end{lstlisting}


% To define a function a Idris, the types for all input values and output values must be specified in the function type signature, connecting by rightarrows. The example below provides the type signature for a \inl{plus} function that adds up two integers. \inl{plus} should take in two integers and returns an integer. 
% \begin{lstlisting}
%       plus : Int -> Int -> Int
% \end{lstlisting}
%A function type takes the forma->b->...->t, wherea,b, and so on, are the input types, and t is the output type. Inputs may also be annotated with names, taking the form(x:a)->(y:b)->...->t.

\subsubsection*{Functions}
To define a function a Idris, the types for all input values and output values must be specified in the function type signature, connecting by right arrows. Specifically, function type is of the form: 
\\\\
\tab <func\_name> : $x_1$ -> $x_2$ -> ... ->  $x_n$
\\\\
where $x_1, x_2, ..., x_{n-1}$ are types for the input values, and $x_n$ is the output type of the function. Input values can be named to provide more information, and also allows each input to be referred to easily later. For instance the type of the \inl{reverse} function below names the first input of type \inl{Type} as \inl{elem}, which specifies that the input and output lists contain elements of same type. 
\begin{lstlisting}
      -- "reverse" reverse a list
      reverse : (elem : Type) -> List elem -> List elem
\end{lstlisting}

An example of calling \inl{reverse} is provided below. The variable \inl{nats} has type \inl{List Nat}. When calling \inl{reverse} on \inl{nats}, the first argument of \inl{reverse} denotes the type of the input list and output list, which is \inl{Nat} in this case, then the output of \inl{(reverse Nat nats)} is also of type \inl{List Nat}, as specified by the type of \inl{reverse_nats}. 
\begin{lstlisting}
      nats : List Nat
      nats = 3 :: 2 :: 1 :: Nil
        
      reverse_nats : List Nat
      reverse_nats = reverse Nat nats
\end{lstlisting}

A function definition is provided on the line below the function type. In Idris, functions are defined by pattern matching, which will be elaborated on later. Here we provide an example for function definition that requires little experience with pattern matching, only aiming to illustrate the syntax for defining functions. The \inl{mult} function defined below multiplies the two input integers. 
\begin{lstlisting}
      -- calculates the multiplication of two input integers `n' and `m'
      mult : Int -> Int -> Int
      mult n m = n * m
\end{lstlisting}

\subsubsection*{Data Types}
User defined data types are supported in Idris. To define a data type, we need to provide the name and type of the data type starting with the keyword \inl{data}, followed by the id and the type of the data type. On the next few lines we define the constructors for this data type. Below provides the definition of the natural number type \inl{Nat} in Idris. 
\begin{lstlisting}
      -- natural number can be either zero, written as `Z', or the successor of another natural number `n', written as `S n'
      data Nat : Type where
        Z : Nat
        S : (n : Nat) -> Nat 
\end{lstlisting}

Idris allows data types to be parameterized. The data type defined below shows that the type constructor \inl{List} takes in a parameter \inl{elem} of type \inl{Type}, which stands for the type of elements in the list, and the type constructed is a list of elements of type \inl{elem}. \inl{List} type has two data constructors, \inl{Nil} and \inl{(::)}. \inl{Nil} builds an empty list of type \inl{List elem}. \inl{(::)} appends a new element \inl{x} of type \inl{elem} to the head of an existing list \inl{xs} of type \inl{List elem}, and builds a new list \inl{x :: xs} of the same type as \inl{xs}. 
\begin{lstlisting}
      -- declaration of List data type in Idris standard library
      data List : (elem : Type) -> Type where
        Nil  : List elem
        (::) : (x : elem) -> (xs : List elem) -> List elem
\end{lstlisting}

\subsubsection*{The \inl{Fin} Type}
We introduce the \inl{Fin} data type here as it is used in our Dijkstra's implementation. The definition of \inl{Fin} data type is provided below. 
\begin{lstlisting}
      data Fin : (n : Nat) -> Type where
          FZ : Fin (S k)
          FS : Fin k -> Fin (S k)
\end{lstlisting}

Given a natural number \inl{n}, \inl{Fin n} captures a finite set of natural numbers that is greater than or equal to zero, and strictly less than \inl{n}. For instance \inl{Fin 3} is the set of natural numbers from 0 to 2. The type of the data constructors \inl{FZ} and \inl{FS} restricts that the input \inl{Nat} \inl{n} must be the successor of some other natural number \inl{k} (i.e., \inl{n} is greater than \inl{Z}), hence we cannot construct a value of type \inl{Fin Z}. \inl{FZ} stands for the first element in the finite set, and \inl{FS n} stands for the (n+1)$^{th}$ element in the set. In our Dijkstra's implementation, we use \inl{Fin} type values as the indices for accessing elements from a list, which helps to ensure safe indexing. 

\subsubsection*{Dependent Types}
Dependent types are types that depend on elements of other types\cite{Bove}. They allow programmers to specify certain properties of data types explicitly in their type. The following example provides a definition of a vector data type, which is indexed by the vector length \inl{len} and parameterized over the element type \inl{elem}.
\begin{lstlisting}
      -- declaration of Vect data type in Idris standard library
      data Vect : (len : Nat) -> (elem : Type) -> Type where
        Nil  : Vect Z elem
        (::) : (x : elem) -> 
               (xs : Vect len elem) -> 
               Vect (S len) elem
\end{lstlisting}

The type \inl{Vect len elem} is dependent on the value of type variables \inl{len} and \inl{elem}, which means two \inl{Vect}s of length 3 and 4 are considered as different types, and two \inl{Vect}s of same length but with element type \inl{Nat} and \inl{Char} are considered as different types. Dependent types allow programmers to obtain more confidence in a function's correctness by specifying its expected behaviors in its type. For instance, consider a function \inl{concat} that concatenates two \inl{Vect}, whose type signature is presented below. 
\begin{lstlisting}
      concat : Vect n elem -> Vect m elem -> resultType
\end{lstlisting}

The output value of \inl{concat} is a vector that concatenates both input vectors, which means its length should be the sum of the length of the two input vectors, i.e., (n+m), hence \inl{resultType} is \inl{Vect (n+m) elem}. The dependent type system helps to ensure the function correctness of \inl{concat} through the Idris type checker. By providing a function type for \inl{concat} that specifies the length of the output \inl{Vect}, if the definition of \inl{concat} does not return a vector of length (n+m), \inl{concat} would fail type check. Take the following definition of \inl{concat} as an example. 
\begin{lstlisting}
      concat : Vect n elem -> Vect m elem -> Vect (n+m) elem
      concat Nil v2 = v2
      concat (x :: xs) ys = concat xs ys
\end{lstlisting}

The type of \inl{concat} specifies that the output value should be a \inl{Vect} of length \inl{(n+m)}, where \inl{n}, \inl{m} are the length of the two input \inl{Vect}, however the definition of \inl{concat} eliminates one element from the input vector \inl{x :: xs} during each recursive call, which is not the expected function behavior. Idris gives the following error message when compiling this function definition: 
\begin{lstlisting}
      Type checking ./Example.idr
      Example.idr:6:23-34:
        |
      6 | concat (x :: xs) ys = concat xs ys
        |                       ~~~~~~~~~~~~
      When checking right hand side of Example.concat with expected type
              Vect (S (plus len m)) Nat

      Type mismatch between
              Vect (plus len m) Nat (Type of concat xs ys)
      and
              Vect (S (plus len m)) Nat (Expected type)

      Specifically:
              Type mismatch between
                      plus len m
              and
                      S (plus len m)
\end{lstlisting}

The error message clearly indicates that the expected return type is \inl{Vect (S (plus len m)) Nat (Expected type)}, which is a vector of length \inl{S (plus len m)}, however the type of \inl{concat xs ys} is \inl{Vect (plus len m) Nat}, whose length is one less than the length of the expected type. As the return type of this definition fails to match with the return type specified in the type of \inl{concat}, it fails to be type checked. A correct implementation of \inl{concat} is provided below. 
\begin{lstlisting}
      concat : Vect n Nat -> Vect m Nat -> Vect (plus n m) Nat
      concat Nil v2 = v2
      concat (x :: xs) ys = x :: (concat xs ys)

      -- definition of `plus' in Idris
      total plus : (n, m : Nat) -> Nat
      plus Z right        = right
      plus (S left) right = S (plus left right)
 \end{lstlisting}

Under the case where the first input argument is \inl{(x :: xs)} (i.e., vector is not empty), the length of the first vector \inl{n} should be the successor of some other natural number \inl{n'} (i.e. \inl{n = S n'}), then \inl{(x :: xs)} has type \inl{Vect (S n')  Nat}, and \inl{xs} has type \inl{Vect n' Nat}. The \inl{concat} function is defined by appending the head of the first input argument, \inl{x}, to the result of \inl{concat xs ys}. As the types of \inl{xs, ys} are \inl{Vect n' Nat}, \inl{Vect m Nat}, the type of \inl{concat xs ys} is \inl{Vect (plus n' m)  Nat}, hence the vector obtained by appending \inl{x} to \inl{concat xs ys} has type \inl{Vect (S (plus n' m))  Nat}. Based on the definition of \inl{plus} in Idris (which is provided above), we see that \inl{S (plus n' m) = plus (S n') m}, which is exactly the expected output type \inl{Vect (plus n m) Nat}, which indicates that the above definition of \inl{concat} type checks. 
\\

% maybe mention implicit parameters somewhere 
The \inl{concat} example above illustrates how dependent types help programmers to ensure function correctness with the Idris type checker. In program verification, dependent types can be used to specify intended behaviors of a program, and thus allowing us to verify its correctness. 


\subsubsection*{Pattern Matching and Totality Checking}
Pattern matching is the process of matching values against specific patterns. In Idris, functions are implemented by pattern matching on possible values of inputs. Continuing with the above example of \inl{concat} function that concatenates two vectors, to define \inl{concat}, we need to provide definitions on all possible values of \inl{Vect}, which can either be \inl{Nil}, i.e., a vector of length zero, or a non-empty vector of the pattern \inl{(x :: xs)}. 
\\

Total function are defined for all possible input values and are guaranteed to terminate. Partial functions are not total, and hence might crash for some inputs. To secure the termination of programs, every function definition in Idris is checked for totality after type checking. However, due to the undecidability of the halting problem, the Idris totality checker is conservative, i.e., is never certain on whether a function is total or not. Based on the Idris Tutorial, Idris decides a function \inl{f} is total if all of the following holds \cite{idris_tutorial}: 
\begin{itemize}
  \item Cover all possible inputs
  \item Be well-founded â€” i.e. by the time a sequence of (possibly mutually) recursive calls reaches f again, it must be possible to show that one of its arguments has decreased.
  \item Not use any data types which are not strictly positive
  \item Not call any non-total functions
\end{itemize}
Specifically, \inl{f} is considered as total if it is defined for all possible input values, for instance given an input of type \inl{Nat}, \inl{f} must cover the cases where it is either \inl{Z} or the successor of another \inl{Nat} (of the form \inl{S n'}); and must have at least one argument that has a property, for instance its value (the \inl{Nat} data type) or length (the \inl{Vect} data type), that is strictly decreasing during each recursive call; the strictly positive restriction is a technical restriction that does not really concern us here, and lastly, \inl{f} cannot call any non-total functions, otherwise \inl{f} might fail to terminate due to the non-total functions called. To illustrate totality checking in Idris, continue with our \inl{concat} function (the definition of \inl{concat} below is not total):
\begin{lstlisting}
    concat : Vect n Nat -> Vect m Nat -> Vect (n+m) Nat
    concat (x :: xs) ys = x :: (concat xs ys)
\end{lstlisting}

We use the \inl{:total} command to check whether the above definition of \inl{concat} is total, and we get the following message:
\begin{lstlisting}
    *Example> :total Example.concat
    Example.concat is not total as there are missing cases
\end{lstlisting}

As \inl{concat} is not defined for the case where the first input vector is \inl{Nil}, hence the Idris totality checker marks \inl{concat} as not total. If we check totality for the correct implementation of \inl{concat} provided under the \inl{Dependent Types} section, we see that Idris considers it as total: 
\begin{lstlisting}
      concat : Vect n Nat -> Vect m Nat -> Vect (n+m) Nat
      concat Nil v2 = v2
      concat (x :: xs) ys = x :: (concat xs ys)

      -- totality checking result for concat
      Type checking ./Example.idr
      *Example> :total Example.concat
      Example.concat is Total
 \end{lstlisting}


\subsubsection*{\textbf{\inl{case}} expressions}
\inl{case} expression can be used to inspect a data value by matching on several cases. The syntax for case expression is as follow: 
\begin{lstlisting}
    case <test> of
        <case 1>  => <expr>
        <case 2>  => <expr>
        ...
        otherwise => <expr>
\end{lstlisting}
where \inl{<test>} is the expression being matched on, followed by all cases in the next few lines. 
Consider the following example that defines a function \inl{findNat} with \inl{case} expressions. \inl{findNat} checks whether a given number \inl{n} is an element of the input vector of \inl{Nat}s. 
\begin{lstlisting}
    findNat : Nat -> Vect m Nat -> Bool
    findNat _ Nil = False
    findNat n (x :: xs) = case (n == x) of 
                            True => True
                            False => findNat n xs
\end{lstlisting}

The base case is when input vector is \inl{Nil}, which indicates that \inl{n} is not an element in the vector. Otherwise we check whether the head of the input vector \inl{(x :: xs)} is equal to \inl{n} with \inl{(n == x)}. Using \inl{case} expression, we can match on the value of \inl{(n == x)}, that if \inl{(n == x)} is True, then \inl{n} is an element of the input vector, \inl{findNat} returns \inl{True}; otherwise we recur on the remaining of the vector \inl{xs} to keep searching. 


\subsubsection*{The \textbf{\inl{with}} Rule}
In a dependently typed language, matching on the resulting value of an intermediate computation can affect what we know about other values. In program implementation and theorem proving, it is a common technique to match on intermediate value in order to obtain more information. Idris provides the \inl{with} rule for this purpose. Consider the following example \inl{checkEvenPrf}: 
\begin{lstlisting}
    checkEven : Nat -> Bool
    checkEven Z = True
    checkEven (S n) = case (checkEven n) of 
                           True => False
                           False => True


    checkEvenPrf : (n : Nat) -> 
                   (checkEven n = True) -> 
                   checkEven (S n) = False
    checkEvenPrf n prf = ?check
\end{lstlisting}

The \inl{checkEven} function checks whether a given \inl{Nat} is even or not. It returns \inl{True} if the input \inl{Nat} is an even number, and returns \inl{False} otherwise. The \inl{checkEvenPrf} function is a proof that if a natural number is even, then its successor must not be even. The type of \inl{checkEvenPrf} describes the premise and conclusion of this proof: given a natural number \inl{n}, if the result of calling \inl{checkEven} on \inl{n} is true (as specified by \inl{checkEven n = True}), then the successor of \inl{n} must not be even, and the result of calling \inl{checkEven} on \inl{(S n)} must be \inl{False}, which is specified by the output type \inl{checkEven (S n) = False}. 
\\


Idris allows holes in a proof which stands for incomplete parts of a program, for instance \st{?check} in the example above is a hole. Idris allows programmers to inspect the type of holes and write functions incrementally. Inspecting the type of \st{check} we get the following:
\begin{lstlisting}
    *Example> :t check
      n : Nat
      prf : checkEven n = True
    --------------------------------------
    check : (case (checkEven n) of   
              True => False 
              False => True) = False
    Holes: Example.check
\end{lstlisting}

The types of arguments of \inl{checkEven} is presented above the dash line in the terminal output, and the expected return type, which is the type of the \inl{check} hole, is presented below the dash line. The information provided by the terminal output shows that the value of \inl{(checkEven n)} might effect the type of \st{check}, which indicates that matching on the value of \st{(checkEven n)} with \st{with} rule might provide more insights in writing this proof, as presented below. 
\begin{lstlisting}
    checkEvenPrf : (n : Nat) -> 
                   (checkEven n = True) -> 
                   checkEven (S n) = False
    checkEvenPrf n prf with (checkEven n) proof nIsEven
      | True = ?checkT
      | False = ?checkF
\end{lstlisting}

In the \st{checkEvenPrf} definition above we use the \inl{with} rule to match on the value of \st{checkEven n}, which can be either \inl{True} or \inl{False} (as \st{checkEven} has return type \inl{Bool}). By postfix the \inl{with} clause with \inl{proof nIsEven}, a proof named \inl{nIsEven} generated by the pattern match will be in scope. By inspecting the type of \inl{checkT} under the cases where \inl{(checkEven n)} is matched as \inl{True}, we get the following information. 
\begin{lstlisting}
    *Example> :t checkT
      n : Nat
      prf : True = True
      nIsEven : True = checkEven n
    --------------------------------------
    checkT : False = False
    Holes: Example.checkF, Example.checkT
\end{lstlisting}

Notice that \inl{nIsEven} is a proof of \inl{True = checkEven n} generated by the pattern match directly. As the \inl{with} rule matches the value of \inl{(checkEven n)} to \inl{True}, and based on the definition of \st{checkEven}, Idris is able to deduce that the value of \inl{checkEven (S n)} should be \inl{False}, and hence the expected type of \inl{checkT} is \inl{False = False} as presented above. When \inl{(checkEven n)} is matched to \inl{False}, the type of \inl{checkF} is as follows: 
\begin{lstlisting}
    *Example> :t checkF
      n : Nat
      prf : False = True
      nIsEven : False = checkEven n
    --------------------------------------
    checkF : True = False
    Holes: Example.checkF, Example.checkT
\end{lstlisting}

As the second argument of \st{checkEvenPrf} indicates that the value of \st{(checkEven n)} should be \inl{True}, Idris is able to deduce that under this case the type of \st{prf} should be \inl{(False = True)}, which is an absurdity, indicating that the value of \inl{(checkEven n)} cannot be \inl{False}. Hence we call the built-in function \st{absurd} on \st{prf} to mark that the case where \st{(checkEven n)} is matched to \inl{False} is impossible. \inl{Refl} is the data constructor for the equality data type \inl{(=)}. \inl{sym} and \inl{trueNotFalse} are built-in functions in Idris that helps with constructing proof with impossible cases in Idris. The complete \st{checkEvenPrf} proof is presented below. 
\begin{lstlisting}
    checkEvenPrf : (n : Nat) -> 
                   (checkEven n = True) -> 
                   checkEven (S n) = False
    checkEvenPrf n prf with (checkEven n) proof nIsEven
      | True = Refl
      | False = absurd $ trueNotFalse (sym prf)
\end{lstlisting}

On the other hand, Idris also restricts programmers from proving something that is not true. Consider the following proof \inl{checkEven_wrong}. 
\begin{lstlisting}
      predN : Nat -> Nat
      predN Z = Z
      predN (S n) = n


      checkEven_wrong : (n : Nat) -> 
                        (checkEven n = True) -> 
                        checkEven (predN n) = False
      checkEven_wrong Z prf = ?caseZ   
      checkEven_wrong (S pn) prf with (checkEven pn) proof pnIsEven
        | True = absurd $ trueNotFalse (sym prf)
        | False = Refl
\end{lstlisting}

The \inl{predN} function calculates the predecessor of a natural number (of type \inl{Nat}). The predecessor of zero \inl{Z} is \inl{Z} itself, and the predecessor of \inl{(S n)} is \inl{n}. Given the definition of \inl{predN}, the function \inl{checkEven_wrong} attempts to prove that for a natural number \inl{n}, if \inl{(checkEven n)} is \inl{True}, as specified by \inl{(checkEven n = True)}, then the predecessor of \inl{n} must not be even, as specified by the output type \inl{checkEven (predN n) = False}. Similar to the \inl{checkEvenPrf} function, the implementation of \inl{checkEven_wrong} under the case where input value \inl{n} is {(S pn)} (the second case) is straightforward, however as we inspect the hole \inl{?caseZ} in the first case where \inl{n} is \inl{Z}, we notice that it is impossible to complete this proof: 
\begin{lstlisting}
      *Example> :t caseZ
        prf : True = True
      --------------------------------------
      caseZ : True = False
\end{lstlisting}

As the type of \inl{caseZ} is \inl{True = False}, which is an absurdity, and there is no information available (above the dash line is what we know for approaching the proof) for us to reach this absurdity, there is no way for us to complete this hole, that the implementation for \inl{checkEven_wrong} can never be completed, which indicates that Idris restricts programmers from writing proofs that are not true. 
 
\subsection{Dijkstra's algorithms}
\subsubsection*{Dijkstra's Algorithm}
Dijkstra's algorithm is a greedy algorithm that finds the shortest path from a given source to all other nodes in a directed graph with weighted edges. It was first introduced in 1959 by Edsger Wybe Dijkstra\cite{Dijkstras}, and it is widely applied in many real-life applications, for instance Internet routing protocols such as the Open Shortest Path First protocol, and a variant of Dijkstra's algorithm is formulated as an instance of the best-first search algorithm in artificial intelligence. 
\\

Dijkstra's algorithm takes in a directed graph with non-negative edge weights, and computes the shortest path distance from one single source node to all other reachable nodes in the graph. The algorithm maintains a list of unexplored nodes and their distance values to the source node. Initially, the list of unexplored nodes contains all nodes in the input graph, and the distance value of all node are set as infinity except for the source node itself, which is set to zero. The algorithm extracts the node $v$ with minimum distance value from the unexplored list during each iteration, and for each neighbor $v'$ of $v$, if the path from source to $v'$ via $v$ contributes a smaller distance value, then the distance value of $v'$ is updated. 

% \subsubsection*{Bellman-Ford Algorithm}
% Bellman-Ford algorithm was first introduced by Alfonso Shimbel in 1955\cite{Shimbel}, and was published by Richard Bellman and Lester Ford, Jr in 1958 and 1956 respectively\cite{Bellman}. The algorithm solves the issue of calculating the minimum distance value from a single source to all other nodes in a given graph, and different from Dijkstra's algorithm, Bellman-Ford algorithm allows negative edge weights in the input graph, and is capable of detecting the existence of negative cycle(a cycle whose edge weights sum up to a negative value). Applications of Bellman-Ford includes routing protocols such as the Routing Information Protocol. 